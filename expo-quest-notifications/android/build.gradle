plugins {
  id 'com.android.library'
  id 'kotlin-parcelize'
  id 'expo-module-gradle-plugin'
}

group = 'host.exp.exponent'
version = '0.32.11'

expoModule {
  canBePublished false
}

def questAppIdConfigField = "\"${project.findProperty('questAppId') ?: ''}\""

android {
  namespace "expo.modules.notifications"
  defaultConfig {
    versionCode 21
    versionName '0.32.11'
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    buildConfigField "String", "META_QUEST_APP_ID", questAppIdConfigField
  }

  buildFeatures {
    buildConfig true
  }
  testOptions {
    unitTests.all { test ->
      testLogging {
        outputs.upToDateWhen { false }
        events "passed", "failed", "skipped", "standardError"
        showCauses true
        showExceptions true
        showStandardStreams true
      }
    }
  }

  flavorDimensions += "device"
  productFlavors {
    quest { dimension "device" }
    mobile { dimension "device" }
  }
}

dependencies {
  implementation 'androidx.core:core:1.17.0'
  implementation 'androidx.lifecycle:lifecycle-runtime:2.9.3'
  implementation 'androidx.lifecycle:lifecycle-process:2.9.3'
  implementation 'androidx.lifecycle:lifecycle-common-java8:2.9.3'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

  questApi("com.meta.horizon.platform.sdk:push-notification-kotlin:0.1.2")
  questImplementation("com.facebook.kotlin.compilerplugins.dataclassgenerate:superclass-jvm:1.0.1")

  // release notes in https://firebase.google.com/support/release-notes/android - cmd + f "Cloud Messaging version"
  implementation 'com.google.firebase:firebase-messaging:24.0.1'

  implementation 'me.leolin:ShortcutBadger:1.1.22@aar'

  if (project.findProject(':expo-modules-test-core')) {
    testImplementation project(':expo-modules-test-core')
    androidTestImplementation project(':expo-modules-test-core')
  }
}
